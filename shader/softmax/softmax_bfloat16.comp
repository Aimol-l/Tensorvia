#version 460

// 需要启动bfloat16扩展 和 int64扩展
#extension GL_EXT_bfloat16 : enable
#extension GL_EXT_shader_explicit_arithmetic_types : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : enable

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout(push_constant) uniform Params {
    int axis_dim;
    int outer_dim;
    int inner_dim;
} params;

layout(std430, binding = 0) buffer InputA {
    bfloat16_t data[];
}src;

layout(std430, binding = 1) buffer InputB {
    bfloat16_t data[];
}dst;


void main() {
    uint idx = gl_GlobalInvocationID.x;
    if(idx >= params.outer_dim * params.inner_dim)
        return;
    
    uint outer_idx = idx / params.inner_dim;
    uint inner_idx = idx % params.inner_dim;

    const uint base_offset = outer_idx * params.axis_dim * params.inner_dim + inner_idx;

    float max_val = float(src.data[base_offset]);

    for(int i =0;i<params.axis_dim;i++){
        float temp = bfloat16_t(src.data[base_offset + i * params.inner_dim]);
        max_val = max(temp,max_val);
    }
    float sum = 0.0;
    for(int i =0;i<params.axis_dim;i++){
        float temp = float(src.data[base_offset + i * params.inner_dim]);
        sum = sum + exp(temp - max_val);
    }
    // 计算！
    for(int i =0;i<params.axis_dim;i++){
        float temp = float(src.data[base_offset + i * params.inner_dim]);
        bfloat16_t res = bfloat16_t(exp(temp - max_val) / sum);
        dst.data[base_offset + i * params.inner_dim] = res;
    }
}